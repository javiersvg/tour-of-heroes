buildscript {
	ext {
		springBootVersion = '2.1.2.RELEASE'
		verifierVersion = '2.1.1.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${verifierVersion}"
	}
}

plugins {
	id 'org.sonarqube' version "2.6"
	id 'com.gradle.build-scan' version '2.1'
	id 'org.asciidoctor.convert' version "1.5.3"
	id 'maven-publish'
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'
apply plugin: 'spring-cloud-contract'

group = 'com.javiersvg'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.11

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${verifierVersion}"
	}
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.security:spring-security-data')
	compile('org.springframework.security:spring-security-oauth2-resource-server')
	compile('org.springframework.security:spring-security-oauth2-client')
	compile('org.springframework.security:spring-security-oauth2-jose')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('org.springframework.data:spring-data-rest-hal-browser')
	compile('org.springframework.data:spring-data-mongodb')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	
    testCompile('de.flapdoodle.embed:de.flapdoodle.embed.mongo')
	testCompile('com.squareup.okhttp3:mockwebserver:3.12.1')

	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	testCompile 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
	testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

contracts {
	packageWithBaseClasses = 'com.javiersvg.tourofheroes'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	dependsOn test
	inputs.dir snippetsDir
	attributes 'snippets': snippetsDir
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

processResources {
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

publishing {
	repositories {
		maven {
			name = "Github"
			url = uri("https://maven.pkg.github.com/javiersvg/tour-of-heroes")
			credentials {
				username = findProperty("github.username")
				password = findProperty("github.token")
			}
		}
	}
	publications {
		register("jar", MavenPublication) {
			from(components["java"])
			pom {
				url.set("https://github.com/javiersvg/tour-of-heroes.git")
			}
		}
	}
}